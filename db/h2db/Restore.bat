@echo off
  setlocal
  call SetVariables.bat
  
  if not exist backup.sql (
    echo.
    echo ----------------------------------------------------------------------
    echo  backup.sql がありません。
    echo  予め Backup.bat でバックアップファイルを作成しておく必要があります。
    echo ----------------------------------------------------------------------
    goto :eof
  )

  echo ------------------------------------------------------------------------------------------
  echo Backup.bat で作成した backup.sql スクリプトを実行し、
  echo バックアップ時点の「%DB_FILE_NAME%.h2.db」を復元します。
  echo.
  echo 注）現在の「%DB_FILE_NAME%.h2.db」のデータは完全に失われます。
  echo ------------------------------------------------------------------------------------------
  set /P YN="実行しますか？ (y) / n ："
  echo.
  if "%YN%"=="y" goto mainProc
  if "%YN%"=="Y" goto mainProc
  if "%YN%"==""  goto mainProc

  echo.
  echo ----------------------------------------------------------------------
  echo  処理をキャンセルしました。
  echo ----------------------------------------------------------------------
  goto :eof

:mainProc
  pushd %~dp0
  if exist %DB_FILE_NAME%.h2.db (
    echo.
    echo ----------------------------------------------------------------------
    echo 「%DB_FILE_NAME%.h2.db」を削除しています。
    echo ----------------------------------------------------------------------
    del %DB_FILE_NAME%.h2.db
  )
  for %%j in (%H2JAR%) do (
    echo.
    echo ----------------------------------------------------------------------
    echo 「%DB_FILE_NAME%.h2.db」を再構築しています。
    echo ----------------------------------------------------------------------
    java -cp %%j org.h2.tools.RunScript -user sa -password sa -url jdbc:h2:file:%DB_FILE_NAME%;AUTO_SERVER=TRUE;MODE=DB2;MV_STORE=FALSE
    if errorlevel 1 goto :errorOccurred
    echo.
    echo ----------------------------------------------------------------------
    echo  データベースの復元が完了しました。
    echo ----------------------------------------------------------------------
    popd
    goto :eof
  )

:errorOccurred
  echo.
  echo ----------------------------------------------------------------------
  echo  エラーが発生しました。
  echo ----------------------------------------------------------------------
  popd
  goto :eof
